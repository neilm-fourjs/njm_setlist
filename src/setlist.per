
TOOLBAR
	ITEM exit
	ITEM accept
	ITEM cancel
	SEPARATOR
	ITEM insert
	ITEM update
	ITEM delete
	ITEM filter
	ITEM filter2
	ITEM prn_songlist
	SEPARATOR
	ITEM new_setlist
	ITEM save_setlist
	ITEM prn_setlist
	ITEM prn_setlist2
	ITEM del_setlist
END -- TB

LAYOUT ( TEXT="Set List", STYLE="main" )
HBOX ( SPLITTER )

VBOX
GRID ( STYLE="darkred" )
{
[b0|master                            ][b1|b2|b3|b4|b5|b6]
}
END -- GRID
TABLE (DOUBLECLICK=addtosetlist)
{
[no1][titl                    ][tim   ][t1][k1]
[no1][titl                    ][tim   ][t1][k1]
[no1][titl                    ][tim   ][t1][k1]
[no1][titl                    ][tim   ][t1][k1]
[no1][titl                    ][tim   ][t1][k1]
                               [tot   ]
}
END -- TABLE
END -- VBOX

VBOX
GRID ( STYLE="darkred" )
{
[setlist:s001               ][s1|s2|s3|s4|s5]
}
END -- GRID
TABLE
{
[no2][atitl                    ][atim  ][t2][k2]
[no2][atitl                    ][atim  ][t2][k2]
[no2][atitl                    ][atim  ][t2][k2]
[no2][atitl                    ][atim  ][t2][k2]
[no2][atitl                    ][atim  ][t2][k2]
                                [atot  ]
}
END -- TABLE
END -- VBOX

GRID
{
<G "Song Detalis"                 >
 [l001      ][f001               ]
 [l002      ][f002               ]
 [l003      ][f03:":":f3]([f003])
 [l004      ][f4]
 [l005      ][f5         ]
 [l006      ][f6]
 [l007      ][f7         ]
 [l008      ][f8 ]
 [l009      ][f9  ]
 [l010      ][f10        ]
 [l011      ][f11           ]
 [l012      ][f12           ]
 [l013      ][f13           ]
 [l014      ][f14           ]
 [l015      ][f15           ]
 [:    :accept:cancel:     :]
}
END -- GRID

END -- HBOX

ATTRIBUTES
BUTTON b0 : exit,TEXT="";
LABEL master : master, TEXT="Master Song List", STYLE="darkred";
BUTTON b1 : insert,TEXT="";
BUTTON b2 : update,TEXT="";
BUTTON b3 : delete,TEXT="";
BUTTON b4 : filter,TEXT="";
BUTTON b5 : filter2,TEXT="";
BUTTON b6 : prn_songlist,TEXT="";

no1 = FORMONLY.no1, TITLE="#";
PHANTOM FORMONLY.sid;
titl = FORMONLY.stitl,TITLE="Title", IMAGECOLUMN=lrn;
PHANTOM FORMONLY.lrn;
PHANTOM FORMONLY.sdur;
tim = FORMONLY.stim,TITLE="Duration", JUSTIFY=RIGHT;
IMAGE t1 = FORMONLY.t1,TITLE="Tempo",AUTOSCALE;
k1 = FORMONLY.k1,TITLE="Key";
AGGREGATE tot = FORMONLY.l_stot,AGGREGATETEXT="Total:",AGGREGATETYPE=PROGRAM;

LABEL setlist : setlist, TEXT="Set List", STYLE="darkred";
COMBOBOX s001 = FORMONLY.m_setlist_id, INITIALIZER=cb_setlist, NOT NULL;

BUTTON s1 : new_setlist,TEXT="";
BUTTON s2 : save_setlist,TEXT="";
BUTTON s3 : prn_setlist,TEXT="";
BUTTON s4 : prn_setlist2,TEXT="";
BUTTON s5 : del_setlist,TEXT="";

no2 = FORMONLY.no2, TITLE="#";
PHANTOM FORMONLY.aid;
atitl = FORMONLY.atitl,TITLE="Title", IMAGECOLUMN=lrn2;
PHANTOM FORMONLY.lrn2;
PHANTOM FORMONLY.adur;
atim = FORMONLY.atim,TITLE="Duration", JUSTIFY=RIGHT;
IMAGE t2 = FORMONLY.t2,TITLE="Tempo",AUTOSCALE;
k2 = FORMONLY.k2,TITLE="Key";
AGGREGATE atot = FORMONLY.l_atot,AGGREGATETEXT="Total:",AGGREGATETYPE=PROGRAM;

LABEL l001 : lab1, TEXT="Title:",JUSTIFY=RIGHT;
LABEL l002 : lab3, TEXT="Artist:",JUSTIFY=RIGHT;
LABEL l003 : lab3, TEXT="Duration:",JUSTIFY=RIGHT;
LABEL l004 : lab4, TEXT="Done Live:",JUSTIFY=RIGHT;
LABEL l005 : lab5, TEXT="Learnt:",JUSTIFY=RIGHT;
LABEL l006 : lab6, TEXT="Recorded:",JUSTIFY=RIGHT;
LABEL l007 : lab7, TEXT="Tempo:",JUSTIFY=RIGHT;
LABEL l008 : lab8, TEXT="Key:",JUSTIFY=RIGHT;
LABEL l009 : lab9, TEXT="Style:",JUSTIFY=RIGHT;
LABEL l010 : lab10, TEXT="Genre:",JUSTIFY=RIGHT;
LABEL l011 : lab11, TEXT="Vocals:",JUSTIFY=RIGHT;
LABEL l012 : lab12, TEXT="Vox FX:",JUSTIFY=RIGHT;
LABEL l013 : lab13, TEXT="Guitar Tuning:",JUSTIFY=RIGHT;
LABEL l014 : lab14, TEXT="Capo:",JUSTIFY=RIGHT;
LABEL l015 : lab15, TEXT="Guitar FX:",JUSTIFY=RIGHT;

PHANTOM FORMONLY.id;
f001 = FORMONLY.titl, SCROLL;
f002 = FORMONLY.artist, SCROLL;
f003 = FORMONLY.dur, NOENTRY;
f03 = FORMONLY.l_min;
f3 = FORMONLY.l_sec;
CHECKBOX f4 = FORMONLY.done_live, VALUECHECKED="Y", VALUEUNCHECKED="N", NOT NULL;
COMBOBOX f5 = FORMONLY.learnt,ITEMS=(("N","No"),("A","Almost"),("Y","Yes")), NOT NULL;
CHECKBOX f6 = FORMONLY.recorded, VALUECHECKED="Y", VALUEUNCHECKED="N", NOT NULL;
COMBOBOX f7 = FORMONLY.tempo,ITEMS=(("S","Slow"),("M","Medium"),("F","Fast")), NOT NULL;
f8 = FORMONLY.songkey, NOT NULL;
COMBOBOX f9 = FORMONLY.style,ITEMS=(("S","Strummed"),("M","Mixed"),("P","Picked")), NOT NULL;
COMBOBOX f10 = FORMONLY.genre,ITEMS=(("F","Folk"),("B","Blues"),("J","Jazz"),("P","Pop"),("R","Rock"),("C","Country")), NOT NULL;
COMBOBOX f11 = FORMONLY.vox,ITEMS=(("T","Tracey"),("N","Neil"),("B","Both")), NOT NULL;
COMBOBOX f12 = FORMONLY.vox_fx,ITEMS=("None","Delay","Marmony"), NOT NULL;
COMBOBOX f13 = FORMONLY.tuning,ITEMS=(("S","Standard"),("DD","Dropped D"),("OD","Open D")), NOT NULL;
SPINEDIT f14 = FORMONLY.capo,VALUEMIN=0, VALUEMAX=12, NOT NULL;
COMBOBOX f15 = FORMONLY.guitar_fx,ITEMS=("None","Chorus","Tremolo","Loop","Chorus-n-Loop","Tremolo-n-Loop","Echo"), NOT NULL;
BUTTON accept : accept;
BUTTON cancel : cancel;

INSTRUCTIONS
SCREEN RECORD tab1 ( FORMONLY.no1 THRU FORMONLY.k1 )
SCREEN RECORD tab2 ( FORMONLY.no2 THRU FORMONLY.k2 )
